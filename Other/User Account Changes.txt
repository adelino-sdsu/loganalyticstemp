SecurityEvent
| where Computer contains "id.sdsu.edu"
| where EventID == 4705 
or EventID == 4720 
or ((EventID == 4722 or EventID == 4723 or EventID == 4724 or EventID == 4725) and Account !contains "$")
or EventID == 4726
or (EventID == 4738 and AccountExpires != "" and AccountExpires != "-")
or EventID == 4740
or EventID == 4767
or (EventID == 4781 and Account !contains "$")
| extend Action_Attempted_extend = case(
EventID==4720,"Created",
EventID==4722,"Enabled",
EventID==4723,"User Changed Password",
EventID==4724,"Password Reset by Admin",
EventID==4725,"Disabled",
EventID==4726,"Deleted",
EventID==4738,"Acct Expiration Change",
EventID==4740,"Locked",
EventID==4767,"Unlocked -whether needed or not",
EventID==4781,"Name Changed",
"Not Applicable")
| extend Result_extend = case(
Level==8,"Succeeded",
Level==16,"Failed",
"Not Applicable")
| extend SecurityID_extend = strcat(SubjectAccount, "\n", TargetAccount)
| extend Old_Account_Name_extend = extract(@".OldTargetUserName.>(.*)<\/Data>", 1, EventData)
| extend New_Account_Name_extend = extract(@".NewTargetUserName.>(.*)<\/Data>", 1, EventData)
| extend PacificTimeStamp = TimeGenerated  -7h
| project PacificTimeStamp, Computer, EventID, Action_Attempted = Action_Attempted_extend, Result = Result_extend, Account_Changed = TargetUserName, SecurityID = SecurityID_extend, Changed_By = SubjectUserName, Old_Account_Name = Old_Account_Name_extend, New_Account_Name = New_Account_Name_extend